include(ExternalProject)
include(SelectLibraryConfigurations)

set(CURL_ROOT ${CMAKE_BINARY_DIR}/external-libs/curl)

list(APPEND CURL_BUILD_ARGS "-DBUILD_CURL_EXE=OFF")
list(APPEND CURL_BUILD_ARGS "-DBUILD_SHARED_LIBS=OFF")
if(WIN32)
   list(APPEND CURL_BUILD_ARGS "-DCURL_STATIC_CRT=ON")
else()
   list(APPEND CURL_BUILD_ARGS "-DCMAKE_POSITION_INDEPENDENT_CODE=ON")
endif()

# Enable only needed protocols
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_FTP=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_LDAP=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_TELNET=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_DICT=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_FILE=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_TFTP=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_HTTP=OFF")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_LDAPS=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_RTSP=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_PROXY=OFF")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_POP3=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_IMAP=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_SMB=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_SMTP=OFF")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_GOPHER=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_DISABLE_MQTT=ON")
list(APPEND CURL_BUILD_ARGS "-DCURL_USE_OPENSSL=ON")

if(CMAKE_TOOLCHAIN_FILE)
   message("-- External lib - CURL : cross compilation (toolchain:${CMAKE_TOOLCHAIN_FILE}, with ${CMAKE_C_COMPILER} and ${CMAKE_CXX_COMPILER})")

   #provide OSXCROSS vars
   set(CURL_PATHCMD)
   if(OSXCROSS_HOST)
      list(APPEND CURL_BUILD_ARGS "-DOSXCROSS_HOST=${OSXCROSS_HOST}")
      list(APPEND CURL_BUILD_ARGS "-DCMAKE_C_COMPILER=${OSXCROSS_HOST}-clang")
      list(APPEND CURL_BUILD_ARGS "-DCMAKE_CXX_COMPILER=${OSXCROSS_HOST}-clang++")
      set(CURL_PATHCMD ${OSXCROSS_HOST}-osxcross-conf)      
   endif()
   
   if(OSXCROSS_TARGET_DIR)
      list(APPEND CURL_BUILD_ARGS "-DOSXCROSS_TARGET_DIR=${OSXCROSS_TARGET_DIR}")
   endif()
   if(OSXCROSS_TARGET)
      list(APPEND CURL_BUILD_ARGS "-DOSXCROSS_TARGET=${OSXCROSS_TARGET}")
   endif()
   if(OSXCROSS_SDK)
      list(APPEND CURL_BUILD_ARGS "-DOSXCROSS_SDK=${OSXCROSS_SDK}")
   endif()
   
   #append all potentials dependencies that external project may need (openssl mostly)
   if(OPENSSL_ROOT)
      list(APPEND CURL_BUILD_ARGS "-DOPENSSL_ROOT_DIR=${OPENSSL_ROOT}")
   endif()
   if(PROTOBUF_ROOT)
      list(APPEND CURL_BUILD_ARGS "-DPROTOBUF_ROOT=${PROTOBUF_ROOT}")
   endif()
   if(SWIG_ROOT)
      list(APPEND CURL_BUILD_ARGS "-DSWIG_ROOT=${SWIG_ROOT}")
   endif()   
   if(Python2_ROOT)
      list(APPEND CURL_BUILD_ARGS "-DPython2_ROOT=${Python2_ROOT}")
   endif()  
   if(Python3_ROOT)
      list(APPEND CURL_BUILD_ARGS "-DPython3_ROOT=${Python3_ROOT}")
   endif()  
   
   list(APPEND CURL_BUILD_ARGS "-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}")
   
   
endif(CMAKE_TOOLCHAIN_FILE)

ExternalProject_Add(curl_build
   GIT_REPOSITORY    https://github.com/curl/curl.git
   GIT_TAG           curl-7_84_0
   GIT_CONFIG        advice.detachedHead=false
   PATCH_COMMAND     ${CURL_PATHCMD}
   PREFIX            ${CURL_ROOT}
   CMAKE_ARGS        ${CURL_BUILD_ARGS}
   UPDATE_COMMAND    ""
   INSTALL_COMMAND   ""
)

SET_PROPERTY(TARGET curl_build PROPERTY FOLDER "External Libs")

ExternalProject_Get_Property(curl_build INSTALL_DIR)
set(CURL_INCLUDE_DIR ${INSTALL_DIR}/src/curl_build/include PARENT_SCOPE)

if(WIN32)
   set(CURL_LIBRARY ${INSTALL_DIR}/src/curl_build-build/lib/$<CONFIG>/libcurl$<$<CONFIG:Debug>:-d>.lib PARENT_SCOPE)
else()
   set(CURL_LIBRARY ${INSTALL_DIR}/src/curl_build-build/lib/libcurl.a PARENT_SCOPE)
endif()
